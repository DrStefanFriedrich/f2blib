/*****************************************************************************
 *
 * Copyright (c) 2019 Stefan Friedrich
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * https://www.eclipse.org/legal/epl-2.0/
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 ******************************************************************************/

plugins {
    id "org.sonarqube" version "2.7"
}

description = "Function to Bytecode Library"

group = 'com.github.drstefanfriedrich.f2blib'
version = '1.0.2'

defaultTasks 'build'

apply plugin: 'java'
apply plugin: 'antlr'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = rootProject.name

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    antlr 'org.antlr:antlr4:4.7.1'
    implementation 'org.antlr:antlr4-runtime:4.7.1',
                   'org.ow2.asm:asm-util:5.0.3',
                   'org.ow2.asm:asm:5.0.4',
                   'com.google.guava:guava:22.0',
                   'org.apache.commons:commons-math3:3.6.1'
    testImplementation 'junit:junit:4.12',
                       'org.hamcrest:hamcrest-all:1.3',
                       'org.mockito:mockito-core:2.12.0'
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-package", "com.github.drstefanfriedrich.f2blib.antlr"]
}

test {
    systemProperty "com.github.drstefanfriedrich.f2blib.performancetest.enabled", System.getProperty("com.github.drstefanfriedrich.f2blib.performancetest.enabled")
    systemProperty "com.github.drstefanfriedrich.f2blib.performancetest.gradle", "true"
    maxHeapSize = '1G'
    jvmArgs '-XX:MaxMetaspaceSize=256m'
    maxParallelForks = 3 * Runtime.runtime.availableProcessors().intdiv(4) ?: 1
}

sonarqube {
    properties {
        property "sonar.login", System.getenv("F2BLIB_SONAR_CLOUD_TOKEN")
    }
}

if (System.getenv("F2BLIB_SONAR_CLOUD_TOKEN")) {
    project.tasks["check"].dependsOn "sonarqube"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

if (System.getenv("F2BLIB_RELEASE_TO_NEXUS")) {

    artifacts {
        archives javadocJar, sourcesJar
    }

    /*
     * Signing works only with GnuPG (gpg) version 1.
     *
     * The keys reside in ~/.gnupg/pubring.gpg as well as ~/.gnupg/secring.gpg.
     * Configuration is stored in ~/.gradle/gradle.properties.
     * All necessary infos can be found at https://central.sonatype.org/pages/ossrh-guide.html
     */
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'F2BLib - Function to Bytecode Library'
                    packaging 'jar'
                    description 'Parse mathematical function expressions, convert them to Java ' +
                            'bytecode, and evaluate them very quickly'
                    url 'https://drstefanfriedrich.github.io/f2blib/'

                    scm {
                        connection 'scm:git:git@github.com:DrStefanFriedrich/f2blib.git'
                        developerConnection 'scm:git:git@github.com:DrStefanFriedrich/f2blib.git'
                        url 'https://github.com/DrStefanFriedrich/f2blib'
                    }

                    licenses {
                        license {
                            name 'Eclipse Public License v2.0'
                            url 'https://www.eclipse.org/legal/epl-2.0/'
                        }
                    }

                    developers {
                        developer {
                            id 'DrStefanFriedrich'
                            name 'Stefan Friedrich'
                        }
                    }
                }
            }
        }
    }

}
